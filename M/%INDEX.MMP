%INDEX ;REL - INDEX & CROSS-REFERENCE ; 02/25/81  8:33 AM ; ;34
 W !!!?21,"C R O S S - R E F E R E N C E R",!
 K  K ^UTILITY($J),^XCR($J) D ^%INDX7 Q:'NRO  S Q="""",(RTN,IOP2,IOP3)=0
 R !!,"DO YOU WISH TO PRINT INDEX?  <YES> => ",IOP1 S IOP1=IOP1=""!(IOP1?1"Y".E) G:'IOP1 BEG
 R !!,"DO YOU WISH TO LIST ROUTINES?  <YES> => ",IOP2 S IOP2=IOP2=""!(IOP2?1"Y".E)
 I IOP2 R !!,"DO YOU ALSO WISH 'STRUCTURED' LISTINGS?  <NO> => ",LC I LC?1"Y".E S IOP2=2
 R !!,"DO YOU WISH TO PRINT ERRORS & WARNINGS?  <YES> => ",IOP3 S IOP3=IOP3=""!(IOP3?1"Y".E) W !
BEG S RTN=$N(^UTILITY($J,RTN)) I RTN=-1 G:IOP1 ^%INDX5 K ^XCR($J),^UTILITY($J) Q
 W !,RTN S X="ZL "_RTN_" S ^XCR($J,RTN,0,1)="""" F LC=1:1:99 S ^(LC)=$T(+LC)"
 X (X_" I ^(LC)="""" K ^(LC) S LC=LC-1 Q")
 S LAB=RTN,LABO=0 I $P(^XCR($J,RTN,0,1)," ",1)'=LAB S ERR=17 D ^%INDX1
 S CCN=0 F TXT=1:1:LC S LIN=^XCR($J,RTN,0,TXT),CCN=CCN+$L(LIN)+2 D LN,ST
 I 'IOP2 K ^XCR($J,RTN,0)
 S ^XCR($J,RTN,0)=CCN
 S LAB=-1,LABO=0
BC S LAB=$N(^XCR($J,RTN,"I",LAB)) G:$E(LAB,1,2)="@(" BC I LAB=-1 G BEG
 G:$D(^XCR($J,RTN,"T",$P(LAB,"+",1))) BC S ERR=14 D ^%INDX1 G BC
LN K V I $P(LIN," ",1)="" S LABO=LABO+1 G CD
 S LAB=$P(LIN," ",1),LABO=0 I $D(^XCR($J,RTN,"T",LAB)) S ERR=15 D ^%INDX1 G CD
 S ^XCR($J,RTN,"T",LAB)=""
CD S I=$L(LIN) I $E(LIN,I)=" " S ERR=13 D ^%INDX1 F CH=1:1 Q:$E(LIN,I)'=" "  S I=I-1,LIN=$E(LIN,1,I)
 S LIN=$P(LIN," ",2,999),GRB=""
EE I LIN="" D ^%INDX2 Q
 S COM=$E(LIN,1) I COM=";" S LIN="" G EE
 I "BCDEFGHIKLOPQRSUVWXZ"'[COM S ERR=1 G ^%INDX1
 D SEP I ARG[":" S COM=$P(ARG,":",1),GRB=GRB_","_$P(ARG,":",2,999)
 E  S COM=ARG
 I $L(COM)>1,$E(COM,1)'="Z",$P($T(CMD),";",2,999)'[(","_COM_",") S ERR=1 G ^%INDX1
 D SEP D:"DEFGKPQRSVWXZ"[$E(COM,1) @$E(COM,1) S:ARG'="" GRB=GRB_","_ARG G EE
D G DG^%INDX4
E Q:ARG=""  S ERR=7 G ^%INDX1
F G FR^%INDX4
G G DG^%INDX4
K G KL^%INDX3
P G ZR^%INDX4
Q Q:ARG=""  S ERR=9 G ^%INDX1
R G RD^%INDX3
S G ^%INDX3
V S ARG="" Q
W G WR^%INDX4
X G XE^%INDX4
Z G ZC^%INDX4
SEP F I=1:1 S CH=$E(LIN,I) D QUOTE:CH=Q Q:"; "[CH
 S ARG=$E(LIN,1,I-1) S:CH=" " I=I+1 S LIN=$E(LIN,I,999) Q
QUOTE S I=I+1,CH=$E(LIN,I) I CH="" S ERR=6 G ^%INDX1
 G QUOTE:CH'=Q S I=I+1,CH=$E(LIN,I) G:CH=Q QUOTE Q
CMD ;,BREAK,CLOSE,DO,ELSE,FOR,GOTO,HALT,HANG,IF,KILL,LOCK,OPEN,PRINT,QUIT,READ,SET,USE,VIEW,WRITE,XECUTE,
ST S R=LAB S:LABO>0 R=R_"+"_LABO
 F LOC="L","G","N","I","X" S S=-1 F I=1:1 S S=$N(V(LOC,S)) Q:S=-1  D SET
 Q
SET S ARG="" I $D(^XCR($J,RTN,LOC,S)) S ARG=^XCR($J,RTN,LOC,S) Q:$L(ARG)>242
 S ARG=ARG_R I V(LOC,S)="+" S ARG=ARG_"(+)"
 S ^XCR($J,RTN,LOC,S)=ARG_"," Q
