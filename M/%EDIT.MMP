%EDIT ; SYSTEM EDITOR - COPYRIGHT (C) 1980 J.J.ALTHOUSE & ASSOC.
 K ^% S ^%=$E($T(%),3,255)
 F I=1:1 S X=$T(%+I) Q:'$L(X)  S Y=$F(X," "),^%($E(X,1,Y-2))=$E(X,Y,255)
 W !,"THE EDITOR IS LOADED" D ^%EDITH Q
 ;
 ; THE CODE THAT FOLLOWS IS LOADED INTO GLOBALS ^%
 ; THE EDITOR IS EXECUTED BY TYPEING 'X ^%'
 ;
% ZM : F %X=1:1 R !,">>",%1,! Q:%1=""  S:%1="A" %1=%0 S %0=%1 X ^%($S($E(%1,$L(%1))=" ":99,$L(%1)=1:2,%1?1"^".E:1,1:0))
0 X %1
1 S %G=%1,%2=@%G,%1=1 X ^%(100) S @%G=%2,%1=""
2 S %1=$F("MDLCSIEBR?G",%1) X:%1 ^%(%1+1) S %1=""
3 W "*MOVE " X ^%(113) W %2,! R "*INSERT BEFORE LABEL: ",%M,! Q:%M=""  ZR @%R ZI %2:@%M
4 R "*DELET LINE(S): ",%2,! ZP @%2 R "OK TO DELET? ",*X W *X Q:X'=89  ZR @%2 W "  DELETED"
5 R "*LIST LINES: ",%2,! S %5=$E(%2,1)="+" ZP:'%5 @%2 X:%5 ^(115)
6 W "*CHANGE " X ^%(113) I $L(%2) F %1=1:1 X ^%(100) Q:'$L(%3)  ZR @%R ZI %2
7 R "*SEARCH FOR: ",%3,! Q:%3=""  F %R=1:1 S @"%2=$T(+%R)" Q:'$L(%2)  I %2[%3 W !,%2,! F %5=1:0 S %5=$F(%2,%3,%5)+0 Q:%5=0  W ?%5-1-$L(%3),">"
8 W "*INSERT BEFORE " X ^%(113) I $L(%2) W %2,! ZM @%R F %X=1:1 R "*",%2,! Q:'$L(%2)  ZI %2
9 R "*REPLACE EVERY: ",%3,! R "*WITH: ",%4,! F %1=1:1 S @"%2=$T(+%1)" Q:'$L(%2)  S %L=$S($E(%2,1)=" ":%L+1,1:0) S:'%L %R=$P(%2," ",1) I %2[%3 W !,%2,! X ^(119) ZR @(%R_"+"_%L) ZI %2 W !,%2
10 R "*BACKUP ROUTINE: ",%R,! ZL @%R S %N=%R_".SAV" O 4:("A":%N) U 4 W %R,! F %R=1:1 S %2=$T(+%R) W %2,! I '$L(%2) C 4:0 U 0 W "*SAVED ON A:",%N Q
11 R "*RESTORE ROUTINE: ",%R,! ZR  S %N=%R_".SAV" O 4:("A":%N) U 4 R %R U 0 W !,%R," FROM ",%N,!! F %N=1:1 U 4 R %2 U 0 W %2,! ZI:$L(%2) %2 I %2="" C 4:0 R !,"OK ? ",*%X W *%X ZR:%X'=89  Q:%X'=89  ZS @%R Q
12 F %X=1000:1 Q:'$D(^%(%X))  W ^%(%X),!
13 R "*GET ROUTINE: ",%R,! ZL @%R ZP
99 S %R=$E(%1,1,$L(%1)-1),@("%2=$T("_%R_")") I $L(%2) F %1=1:1 X ^%(100) Q:'$L(%3)  ZR @%R ZI %2
100 W:%1=1 %2,! R "*REPLACE: ",%3,! X:%3["..." ^%(114) I $L(%3),%2[%3 S %5=$F(%2,%3),%6=$E(%2,1,%5-$L(%3)-1) W %6 R %4 S %7=$E(%2,%5,255),%2=%6_%4_%7 W %7,!
113 S %2="" F %1=1:1 R "LABEL: ",%R,! Q:%R=""  S @("%2=$T("_%R_")") Q:$L(%2)  W %R," INVALID LABEL",!
114 S %5=$F(%3,"..."),%31=$E(%3,1,%5-4),%32=$E(%3,%5,255) I %2[%31,%2[%32 S %51=$F(%2,%31),%52=$F(%2,%32,%51) S:%52=%51 %52=256 S %3=$E(%2,%51-$L(%31),%52-1)
115 S %3=$P(%2,":",2) S:%3="" %3=%2 F %4=%2:1:%3 W !,@("$T(+"_%4_")")
119 F %5=1:0 S %5=$F(%2,%3,%5)+0 Q:%5=0  S %2=$E(%2,1,%5-$L(%3)-1)_%4_$E(%2,%5,255) W ?%5-1-$L(%3),">"
