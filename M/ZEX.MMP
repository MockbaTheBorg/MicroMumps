ZEX	;ZIM;4 OCT 77;COOPER'S AEROBICS POINTS SYSTEM (1970)
	KILL  SET SUM=0,DAY=$PIECE($HOROLOG,",",1)
	READ !!,"IS YOUR EXERCISE DATA FOR TODAY? (YES OR NO) ",X,!!
	IF X'?1"Y".E READ "HOW MANY DAYS AGO? ",X,!! SET DAY=DAY-X
	IF $DATA(^ZEX(DAY)) WRITE "CHANGE FROM ",^(DAY)," POINTS? " READ X,! IF X'["Y" QUIT
RUN	READ "HOW MANY MILES DID YOU RUN? ",MILES,! IF 'MILES GOTO SWIM
	IF MILES>100 WRITE "IMPOSSIBLE",! GOTO RUN
	IF MILES>2 WRITE "    VERY GOOD",!
	READ "HOW MANY MINUTES DID THAT TAKE? ",MINS,!
	IF 'MINS DO ZERO GOTO RUN
	SET T=MINS/MILES IF T<8 WRITE "THAT'S FAST!",!
	IF MILES<2,T>19.99 SET POINTS=0 GOTO ERUN
	SET POINTS=$SELECT(T<6.5:6,T<8:5,T<10:4,T<12:3,T<14.5:2,T<20:1,1:.5)*MILES
ERUN	DO SET GOTO RUN
SWIM	READ "HOW MANY YARDS DID YOU SWIM? ",YARDS,! IF 'YARDS GOTO BIKE
	IF YARDS>500 WRITE "WELL DONE",!
	READ "HOW MANY MINUTES DID YOU TAKE? ",MINS,! IF 'MINS DO ZERO GOTO SWIM
	SET T=100*MINS/YARDS
	IF YARDS<300,T>3.3 SET POINTS=0 GOTO ESWIM
	SET POINTS=$SELECT(T<1.667:1.25,T<2.5:.85,T<3.333:.625,1:.2)*YARDS/100
	IF POINTS<1 SET POINTS=0
ESWIM	DO SET GOTO SWIM
BIKE	READ "HOW MANY MILES DID YOU CYCLE? ",MILES,! IF 'MILES GOTO END
	IF MILES>6 WRITE "GOOD",!
	READ "HOW MANY MINUTES DID YOU TAKE? ",MINS,! IF 'MINS DO ZERO GOTO BIKE
	SET T=MINS/MILES
	IF MILES<5,T>5.99 SET POINTS=0 GOTO EBIKE
	SET POINTS=$SELECT(T<3:1.5,T<4:1,T<6:.5,1:.22)*MILES
EBIKE	DO SET GOTO BIKE
END	WRITE:DAY=+$HOROLOG "TODAY'S " WRITE "POINTS = ",SUM,!
	SET ^ZEX(DAY)=SUM WRITE "GOODBYE",! KILL  QUIT
SET	SET SUM=SUM+POINTS
	WRITE "YOU JUST ADDED ",POINTS," POINT" IF POINTS'=1 WRITE "S"
	WRITE " AND YOUR TOTAL IS ",SUM,!,"ENTER MORE",!! QUIT
ZERO	WRITE "YOU CAN'T DO THINGS IN NO TIME",!! QUIT
%	WRITE "ZEX" ZSTORE ZEX Q
