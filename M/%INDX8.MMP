%INDX8 ; STRUCTURED INDEX ; 26 APR 82  4:46 PM
 W #!,RTN,!! S Q="""",LO=0
 F LC=1:1 Q:'$D(^XCR($J,RTN,0,LC))  S LIN=^(LC) D CD
 Q
CD S LAB=$P(LIN," ",1),LIN=$P(LIN," ",2,999),IDT=10,LO=$S(LAB="":LO+1,1:0)
 W $S(LAB'="":LAB,1:" +"_LO)
 G:LIN'[";" EE S STR=1,L=";",ARG=LIN D LOOP I CH'=";" G EE
 W ?10,$E(LIN,I,999),! Q:I<2  S LIN=$E(LIN,1,I-2)
EE I LIN="" Q
 S COM=$E(LIN,1),EOC=0 I "BCDEFGHIKLOPQRSUVWXZ"'[COM G ERR
 D SEP I ARG[":" S OLD=$P(ARG,":",1),COM="IF",ARG=$P(ARG,":",2) D GRB S IDT=IDT+4,ARG=OLD,EOC=4
 S COM=ARG I $L(COM)>1,$E(COM,1)'="Z",$P($T(CMD),";",2,999)'[(","_COM_",") G ERR
 I $E(COM,1)="Z" S X=COM
 E  S COM=$E(COM,1) F I=2:1 S X=$P($T(CMD),",",I) Q:X=""  Q:$E(X,1)=COM
 S:COM="H"&(ARG'="") X="HANG" S COM=X,X=$E(X,1)
 D SEP D GRB:"BCHKLOPQRUVWZ"[X,SET:X="S",DGX:"DGX"[X,IFE:"IE"[X,FOR:X="F" S:EOC IDT=IDT-EOC G EE
GRB I ARG["$" F I=1:1 S CH=$E(ARG,I) Q:CH=""  D QUOTE:CH=Q I CH="$" D FUN
 W ?IDT,COM," ",ARG,! Q
FUN F J=I+1:1 Q:$E(ARG,J)'?1U
 S X=$E(ARG,I+1,J-1),L=$L(X),CH=$E(ARG,I+1),TY=$S($E(ARG,J)="(":"FNC",1:"SPC")
 I $S(TY="FNC":"ACDEFJLNOPRSTVZ",1:"HIJSTXYZ")'[CH G ERR
 I $L(X)>1,CH'="Z",$P($T(@TY),";",2,999)'[(","_X_",") G ERR
 I CH'="Z" F PC=2:1 S JJ=$P($T(@TY),",",PC) Q:JJ=""  I $E(JJ,1)=CH S X=JJ Q
 Q:L=$L(X)  S ARG=$E(ARG,1,I)_X_$E(ARG,J,999),I=I+$L(X)-L Q
ERR W !,"*** ERROR ***",! Q
IFE I ARG="" S:X="I" ARG="$TEST" W ?IDT,COM," ",ARG,! S IDT=IDT+4 Q  ;JAS VMTH 4/26/82
SET S STR=1,L="," D LOOP S SAV=ARG,ARG=$E(ARG,1,I-1),IP=I+1
 D GRB S ARG=$E(SAV,IP,999) S:COM="IF" IDT=IDT+4 Q:ARG=""  G SET
FOR D GRB S IDT=IDT+4 Q
DGX S STR=1,L=":," D LOOP I CH="" G GRB
 I CH="," S SAV=ARG,ARG=$E(ARG,1,I-1),IP=I+1 D GRB G D1
 S SAV=ARG,STR=I+1,L="," D LOOP S IP=I+1
 S OLD=COM,ARG=$E(ARG,STR,I-1),COM="IF" D GRB
 S IDT=IDT+4,ARG=$E(SAV,1,STR-2),COM=OLD D GRB S IDT=IDT-4
D1 S ARG=$E(SAV,IP,999) Q:ARG=""  G DGX
LOOP F I=STR:1 S CH=$E(ARG,I) D QUOTE:CH=Q,PAREN:CH="(" Q:L[CH
 Q
PAREN S PC=1
P2 S I=I+1,CH=$E(ARG,I) I PC=0 Q
P4 G P2:"()"""'[CH I CH="" G ERR
 I CH=Q D QUOTE G P4
 S PC=PC+$S(CH="(":1,1:-1) G P2
QUOTE S I=I+1,CH=$E(ARG,I) I CH="" G ERR
 G QUOTE:CH'=Q S I=I+1,CH=$E(ARG,I) G:CH=Q QUOTE Q
SEP F I=1:1 S CH=$E(LIN,I) D SEPQ:CH=Q Q:"; "[CH
 S ARG=$E(LIN,1,I-1) S:CH=" " I=I+1 S LIN=$E(LIN,I,999) Q
SEPQ S I=I+1,CH=$E(LIN,I) I CH="" G ERR Q
 G SEPQ:CH'=Q S I=I+1,CH=$E(LIN,I) G:CH=Q SEPQ Q
CMD ;,BREAK,CLOSE,DO,ELSE,FOR,GOTO,HALT,HANG,IF,KILL,LOCK,OPEN,PRINT,QUIT,READ,SET,USE,VIEW,WRITE,XECUTE,
FNC ;,ASCII,CHAR,DATA,EXTRACT,FIND,JUSTIFY,LENGTH,NEXT,ORDER,PIECE,RANDOM,SELECT,TEXT,VIEW,
SPC ;,HOROLOG,IO,JOB,STORAGE,TEST,X,Y,
 ;
S ;STRUCTURED LISTINGS ONLY; JAS VMTH; 25MAR82
 S %JO=$J D ^%RSET,^%IS,INT^%D,INT^%T U IO S Q="""",RTN=0,LOAD="ZL @RTN K XX F LC=1:1 Q:$T(+LC)=""""  S XX(LC)=$T(+LC)" U IO D RTN W # U 0 C:IO-$I IO K XX Q
RTN S RTN=$N(^UTILITY(%JO,RTN)) Q:RTN<0  D L G RTN
L X LOAD S XX(0)=LC-1
LR W #,RTN,!! F LC=1:1:XX(0) S LIN=XX(LC) D CD
